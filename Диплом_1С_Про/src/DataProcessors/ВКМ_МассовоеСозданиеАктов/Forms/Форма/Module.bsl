// @strict-types

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументыРеализации(Команда)
	
	СоздатьНаСервере();	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура СоздатьНаСервере()
	
	Если Объект.Период.ДатаНачала = Дата("01.01.0001 0:00:00") Или Объект.Период.ДатаОкончания = Дата("01.01.0001 0:00:00") Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не задан период");
		Возврат;
		
	КонецЕсли;
	
	НачалоПериода = НачалоМесяца(Объект.Период.ДатаНачала);
	КонецПериода = КонецМесяца(Объект.Период.ДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ПОМЕСТИТЬ ВТ_Договоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|	И &НачалоПериода МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Договор.Ссылка КАК Реализации
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Договор.ВидДоговора = &ВидДоговора
	|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеализацияТоваровУслуг.ПометкаУдаления = Ложь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Договоры.Договор.Ссылка КАК ДоговорСсылка
	|ИЗ
	|	ВТ_Договоры КАК ВТ_Договоры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
	|		ПО (ВТ_Реализации.Реализации.Ссылка = ВТ_Договоры.Договор.Ссылка)
	|ГДЕ
	|	ВТ_Реализации.Реализации.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВидДоговора", ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание"));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Результат = Запрос.Выполнить();
	Выгрузка = Результат.Выгрузить();
	
	Если Результат.Пустой() Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("За указанный период документы уже созданы");
		
	Иначе
		
		Для Каждого Строка Из Выгрузка Цикл
			
			НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			НовыйДокумент.Контрагент = Строка.ДоговорСсылка.Владелец;
			НовыйДокумент.Договор = Строка.ДоговорСсылка.Ссылка;
			НовыйДокумент.Организация = Строка.ДоговорСсылка.Организация;
			НовыйДокумент.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			НовыйДокумент.Дата = КонецПериода;
			НовыйДокумент.ВыполнитьАвтозаполнение(НовыйДокумент.Договор);
			
			Если ЗначениеЗаполнено(НовыйДокумент.Услуги) Тогда
				
				НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
				НоваяСтрока= Объект.СписокРеализаций.Добавить();
				НоваяСтрока.Договор = НовыйДокумент.Договор.Ссылка;
				НоваяСтрока.РеализацияПоДоговору = НовыйДокумент.Ссылка;
				
			Иначе
				
				ТекстСообщения = СтрШаблон("Клиенту %1 по договору %2 за %3 услуги не предоставлялись",НовыйДокумент.Контрагент,
					НовыйДокумент.Договор, Формат(КонецПериода, "ДФ='MMMM yyyy'"));
					
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
