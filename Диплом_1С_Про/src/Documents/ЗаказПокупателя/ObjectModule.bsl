// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ЗадолженностьПокупателей.Записывать = Истина;
	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	
	Движение = Движения.ЗадолженностьПокупателей.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Сумма = СуммаДокумента;

	ДвижениеОбработкаЗаказов = Движения.ОбработкаЗаказов.Добавить();
	ДвижениеОбработкаЗаказов.Период = Дата;
	ДвижениеОбработкаЗаказов.Контрагент = Контрагент;
	ДвижениеОбработкаЗаказов.Заказ = Ссылка;
	ДвижениеОбработкаЗаказов.Договор = Договор;
	ДвижениеОбработкаЗаказов.СуммаЗаказа = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		
		ДвижениеПродажи = Движения.Продажи.Добавить();
		ДвижениеПродажи.Период = Дата;
		ДвижениеПродажи.Контрагент = Контрагент;
		ДвижениеПродажи.Номенклатура = ТекСтрокаТовары.Номенклатура;
		ДвижениеПродажи.Сумма = ТекСтрокаТовары.Сумма;
		
	КонецЦикла;

	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		ДвижениеПродажи = Движения.Продажи.Добавить();
		ДвижениеПродажи.Период = Дата;
		ДвижениеПродажи.Контрагент = Контрагент;
		ДвижениеПродажи.Номенклатура = ТекСтрокаУслуги.Номенклатура;
		ДвижениеПродажи.Сумма = ТекСтрокаУслуги.Сумма;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
